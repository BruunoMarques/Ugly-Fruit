class Delegation
types
	public DelegationName = seq1 of char;
	public LocationName = seq1 of char;
	public UserSet = set of User;
	public Weight = real;
	public ProductSet = map Product to Weight;
	public ProducerSet = set of Producer;
	public UserBaskets = map User to Basket;
	
values
-- TODO Define values here

instance variables
	public name: DelegationName;
	public userCapacity: nat1;
	public location: LocationName;
	public users: UserSet := {};
	public producers: ProducerSet := {};
	public pendingUsers: UserSet := {};
	public userBaskets: UserBaskets := { |-> };
	inv getUsersWeight() <= getMaxWeight();				-- a capacidade de utilizadores registados na delegacao nunca pode ser excedida
	
operations

	public Delegation : DelegationName*LocationName*nat1 ==> Delegation
		Delegation(nm,ln,uc)==(
			name := nm;
			location := ln;
			userCapacity := uc;
			return self;
		)
		post name = nm and location = ln and userCapacity = uc;
	
	public registerUser : User*Basket`Size ==> ()								-- retorna verdadeiro se o registo do utilizador foi bem sucedido
		registerUser(user, basketSize)==(
			dcl tmp : User := new User("tmp");
			tmp.setBasketSize(<large>);															-- verifica se a delegacao tem capacidade para satisfazer os pedidos de cestas de utilizadores
			
			if ((card users >= userCapacity) or (getrequiredWeight(users union {tmp}) > getMaxWeight()) or productsPerBasket() < 8)
			then (
				pendingUsers := pendingUsers union {user};
			)
			else (
				users := users union {user};
				
				IO`print("Registered user ");
				IO`print(user.name);
				IO`print(" on delegation ");
				IO`println(self.name);
				
				user.setBasketSize(basketSize);
				user.setDelegation(self);
			);
		)
		post card users <= userCapacity and (user in set users or user in set pendingUsers) and getUsersWeight() <= getMaxWeight();
		
	public isUserRegistered : User ==> bool
		isUserRegistered(user)==(
			if user in set users
			then return true
			else return false;
		);
		
	public removeUser(user: User) ==
  	users := users \ {user}
  	pre users <> {} and user in set users
  	post user not in set users;

	public registerProducer : Producer ==> ()
		registerProducer(producer)==(
			producers := producers union {producer};
			IO`print("Registered producer ");
			IO`print(producer.name);
			IO`print(" on delegation ");
			IO`println(self.name);
		)
		pre producer not in set producers
		post producers <> {};
	
	public getProducts : () ==> ProductSet
		getProducts()==(
			dcl P : ProductSet := { |-> };
			for all producer in set producers do (
				for all product in set dom producer.products do (
					if product in set dom P
					then P(product) := P(product) + producer.products(product)
					else P := P munion { product |-> producer.products(product)};
				);
			);
			return P;
		);
		
	public createBaskets : () ==> ()
		createBaskets()==(
			for all user in set users do (
				dcl bsk : Basket := new Basket(user.basketSize);
				dcl minWeight : real := 0;
				dcl maxWeight : real := 0;
				dcl variety : real := 0;
				
				if (user.basketSize = <small>)
				then (minWeight := 3; maxWeight := 4; variety := 7)
				else (minWeight := 6; maxWeight := 8; variety := 8);
				
				createBasketsAux(user, bsk, minWeight, maxWeight, variety);
			);
		);
		
		public createBasketsAux : User*Basket*real*real*real ==> ()
			createBasketsAux(user, bsk, minWeight, maxWeight, variety)==(
				dcl v : real := 0;
				dcl variedProducts : set of Product := {};
				
				while(bsk.weight < minWeight or variety <> v) do (
					for all producer in set producers do (
						for all product in set dom producer.products do (
							dcl w : real := 0.5;
							dcl deletedWeight : real := 0;
	
							deletedWeight := producer.removeStock(product, w);
							if (deletedWeight > 0)
							then (
								bsk.addProduct(product, deletedWeight);
								
								if (product not in set variedProducts)
								then (variedProducts := variedProducts union {product}; v := v + 1);
	
								if(bsk.weight >= minWeight and bsk.weight <= maxWeight and variety = v)
								then (
									userBaskets := userBaskets munion {user |-> bsk};
									
									if user.basketSize = <small>
									then IO`print("Small ")
									else IO`print("Large ");
									
									IO`print("basket created for user ");
									IO`print(user.name);
									IO`print(" on delegation ");
									IO`println(self.name);							
									IO`println("Basket contents: ");
									IO`println(bsk);
									IO`println("");
									
									return);
								);
							);
						);
					);
				);
		
/*	public removeProduct : Product*real ==> ()
		removeProduct(product,val)==(
    products(product) := (products(product) - val); 
    )
    pre products(product)- val > 0
    post products(product) >= 0;
*/    
	
	public pure getMaxWeight : () ==> real
	getMaxWeight()==
		getProducerslWeight(producers);
		
	public pure getUsersWeight : () ==> real
	getUsersWeight()==
		getrequiredWeight(users);
		
	public productsPerBasket: () ==> nat
		productsPerBasket()==(
			if(card users > 0)
				then checkVariety(getProducts(), card users, 8,0)
				else return 8;
		);

functions		
	public getProducerslWeight : ProducerSet -> real
	getProducerslWeight(producers)==(
	  if producers = {} then 0
		else let p in set producers in p.getWeight() + getProducerslWeight(producers\{p} ) 
		);
			    	            	
	public getrequiredWeight : UserSet -> real
	getrequiredWeight(users)==(
	  if users = {} then 0.0
		else let u in set users in 
		if u.basketSize = <small> then 4.0 + getrequiredWeight(users\{u})
		else 8 + getrequiredWeight(users\{u})
		);
		
	public checkVariety : ProductSet*nat*nat*nat -> nat
		checkVariety(products, basketNum, variety, rest)==(
		  if products = {|->} then variety + floor (rest/basketNum)  
			else let p in set dom products in 
			if products(p) / 0.5 >= basketNum then checkVariety({p} <-: products, basketNum, variety + 1, rest)
			else checkVariety({p} <-: products, basketNum, variety, rest + floor(products(p) / 0.5)) 
			)
			pre basketNum > 0;
		
traces
-- TODO Define Combinatorial Test Traces here
end Delegation